#
# A simple theme inspired by the Sorin and PeepCode themes.
#
# Authors:
#   Peter Garland <pngarland@gmail.com>
#
#
# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
#

# Load dependencies.
pmodload 'helper'

function prompt_sherpa_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_sherpa_pwd="$MATCH"
    unset MATCH
  else
    _prompt_sherpa_pwd="%F{red}$(date '+%I:%M') %F{14}${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%} %F{4}"
  fi
}

function prompt_sherpa_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_sherpa_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby version information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_sherpa_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info and ruby-info before each command.
  add-zsh-hook precmd prompt_sherpa_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %F{green}»%f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%F{magenta}%s%f'
  zstyle ':prezto:module:git:info:branch' format '%F{blue}%b%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{green}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  zstyle ':prezto:module:git:info:commit' format '%F{blue}%.7c%f'
  zstyle ':prezto:module:git:info:position' format '%F{blue}%p%f'
  zstyle ':prezto:module:git:info:keys' format \
    'rprompt' ' $(coalesce "%b" "%p" "%c")%s%C%D'
  zstyle ':prezto:module:git:info:keys' format \
    'commit_sha' '$(coalesce "%c")'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format ' %F{yellow}%v%f'

  # Define prompts.
  PROMPT='%F{cyan}${_prompt_sherpa_pwd}${git_info:+${(e)git_info[commit_sha]}}%f${editor_info[keymap]} '
  RPROMPT='${git_info:+${(e)git_info[rprompt]}}'
}

prompt_sherpa_setup "$@"
